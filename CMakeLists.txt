cmake_minimum_required(VERSION 3.20)

if(NOT DEFINED LIVEKIT_VERSION OR LIVEKIT_VERSION STREQUAL "")
  set(LIVEKIT_VERSION "0.1.0")
endif()

project(livekit VERSION ${LIVEKIT_VERSION} LANGUAGES C CXX)

set(LIVEKIT_ROOT_DIR ${CMAKE_CURRENT_SOURCE_DIR})
set(LIVEKIT_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR})

option(LIVEKIT_BUILD_EXAMPLES "Build LiveKit examples" OFF)
option(LIVEKIT_USE_VCPKG "Use vcpkg for dependency management" ON)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

if(CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
  set(LIVEKIT_IS_TOPLEVEL TRUE)
else()
  set(LIVEKIT_IS_TOPLEVEL FALSE)
endif()

if(LIVEKIT_IS_TOPLEVEL)
  if(WIN32)
    set(PLATFORM_DIR "windows-x64")
  elseif(APPLE)
    set(PLATFORM_DIR "macos-universal")
  elseif(UNIX)
    set(PLATFORM_DIR "linux-x64")
  else()
    set(PLATFORM_DIR "unknown")
  endif()

  if(WIN32)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib/${PLATFORM_DIR}/$<LOWER_CASE:$<CONFIG>>)
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib/${PLATFORM_DIR}/$<LOWER_CASE:$<CONFIG>>)
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin/$<LOWER_CASE:$<CONFIG>>)
  else()
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib/${PLATFORM_DIR})
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib/${PLATFORM_DIR})
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
  endif()
endif()

if(MSVC)
  # Use dynamic CRT (/MD) for compatibility with Qt and other /MD libraries.
  # The livekit_ffi.dll isolates the /MT libwebrtc dependency, so livekit.lib
  # can safely use /MD.
  set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>DLL")
  add_compile_options(/utf-8)
endif()

set(FFI_PROTO_DIR ${LIVEKIT_ROOT_DIR}/client-sdk-rust/livekit-ffi/protocol)
set(FFI_PROTO_FILES
    ${FFI_PROTO_DIR}/handle.proto
    ${FFI_PROTO_DIR}/ffi.proto
    ${FFI_PROTO_DIR}/participant.proto
    ${FFI_PROTO_DIR}/room.proto
    ${FFI_PROTO_DIR}/track.proto
    ${FFI_PROTO_DIR}/video_frame.proto
    ${FFI_PROTO_DIR}/audio_frame.proto
    ${FFI_PROTO_DIR}/e2ee.proto
    ${FFI_PROTO_DIR}/stats.proto
    ${FFI_PROTO_DIR}/data_stream.proto
    ${FFI_PROTO_DIR}/rpc.proto
    ${FFI_PROTO_DIR}/track_publication.proto
)
set(PROTO_BINARY_DIR ${LIVEKIT_BINARY_DIR}/generated)
file(MAKE_DIRECTORY ${PROTO_BINARY_DIR})

find_package(Protobuf CONFIG REQUIRED)

add_library(livekit_proto OBJECT ${FFI_PROTO_FILES})
target_include_directories(livekit_proto PRIVATE
  "$<BUILD_INTERFACE:${PROTO_BINARY_DIR}>"
  ${Protobuf_INCLUDE_DIRS}
)
target_link_libraries(livekit_proto PRIVATE protobuf::libprotobuf)

protobuf_generate(
  LANGUAGE cpp
  TARGET livekit_proto
  PROTOC_OUT_DIR ${PROTO_BINARY_DIR}
  IMPORT_DIRS ${FFI_PROTO_DIR}
)

set(GENERATED_INCLUDE_DIR "${LIVEKIT_BINARY_DIR}/generated")
file(MAKE_DIRECTORY "${GENERATED_INCLUDE_DIR}")

set(GIT_COMMIT "unknown")
find_package(Git QUIET)
if(GIT_FOUND AND EXISTS "${LIVEKIT_ROOT_DIR}/.git")
  execute_process(
    COMMAND "${GIT_EXECUTABLE}" rev-parse --short HEAD
    WORKING_DIRECTORY "${LIVEKIT_ROOT_DIR}"
    OUTPUT_VARIABLE GIT_COMMIT
    OUTPUT_STRIP_TRAILING_WHITESPACE
    ERROR_QUIET
  )
endif()

string(TIMESTAMP BUILD_DATE "%Y-%m-%d %H:%M:%S")
set(GENERATED_COMMENT "This file was auto-generated by CMake on ${BUILD_DATE}. Do NOT edit manually.")

configure_file(
  "${LIVEKIT_ROOT_DIR}/build.h.in"
  "${GENERATED_INCLUDE_DIR}/build.h"
  @ONLY
)


find_program(CARGO_EXECUTABLE NAMES cargo REQUIRED)

set(RUST_ROOT ${LIVEKIT_ROOT_DIR}/client-sdk-rust)

set(RUN_CARGO_SCRIPT ${LIVEKIT_BINARY_DIR}/run_cargo.cmake)
file(WRITE ${RUN_CARGO_SCRIPT}
"if(NOT DEFINED CFG)
  set(CFG Debug)
endif()
if(NOT DEFINED RUST_ROOT)
  message(FATAL_ERROR \"RUST_ROOT not set\")
endif()
if(NOT DEFINED CARGO)
  message(FATAL_ERROR \"CARGO not set\")
endif()

set(ARGS build)
if(NOT CFG STREQUAL \"Debug\")
  list(APPEND ARGS --release)
endif()

message(STATUS \"[run_cargo.cmake] CFG=\${CFG}  CARGO=\${CARGO}  PROTOC=\${PROTOC_PATH}\")
set(ENV{PROTOC} \"\${PROTOC_PATH}\")

execute_process(
  COMMAND \"\${CARGO}\" \${ARGS}
  WORKING_DIRECTORY \"\${RUST_ROOT}\"
  RESULT_VARIABLE rv
)
if(rv)
  message(FATAL_ERROR \"cargo build failed with code: \${rv}\")
endif()
")

# Use SHARED library (DLL) on Windows to isolate /MT CRT from consuming /MD apps
if(WIN32)
  add_library(livekit_ffi SHARED IMPORTED GLOBAL)
  # DLL import libraries (.dll.lib)
  set(RUST_IMPLIB_DEBUG   "${RUST_ROOT}/target/debug/livekit_ffi.dll.lib")
  set(RUST_IMPLIB_RELEASE "${RUST_ROOT}/target/release/livekit_ffi.dll.lib")
  # Actual DLL files
  set(RUST_DLL_DEBUG      "${RUST_ROOT}/target/debug/livekit_ffi.dll")
  set(RUST_DLL_RELEASE    "${RUST_ROOT}/target/release/livekit_ffi.dll")
  # For OUTPUT dependency checking
  set(RUST_LIB_DEBUG   "${RUST_IMPLIB_DEBUG}")
  set(RUST_LIB_RELEASE "${RUST_IMPLIB_RELEASE}")
  
  set_target_properties(livekit_ffi PROPERTIES
    IMPORTED_CONFIGURATIONS "Debug;Release;RelWithDebInfo;MinSizeRel"
    # IMPORTED_LOCATION = DLL file
    IMPORTED_LOCATION_DEBUG          "${RUST_DLL_DEBUG}"
    IMPORTED_LOCATION_RELEASE        "${RUST_DLL_RELEASE}"
    IMPORTED_LOCATION_RELWITHDEBINFO "${RUST_DLL_RELEASE}"
    IMPORTED_LOCATION_MINSIZEREL     "${RUST_DLL_RELEASE}"
    # IMPORTED_IMPLIB = Import library for linking
    IMPORTED_IMPLIB_DEBUG            "${RUST_IMPLIB_DEBUG}"
    IMPORTED_IMPLIB_RELEASE          "${RUST_IMPLIB_RELEASE}"
    IMPORTED_IMPLIB_RELWITHDEBINFO   "${RUST_IMPLIB_RELEASE}"
    IMPORTED_IMPLIB_MINSIZEREL       "${RUST_IMPLIB_RELEASE}"
    INTERFACE_INCLUDE_DIRECTORIES    "${RUST_ROOT}/livekit-ffi/include"
  )
else()
  # Non-Windows: use static library as before
  add_library(livekit_ffi STATIC IMPORTED GLOBAL)
  set(RUST_LIB_DEBUG   "${RUST_ROOT}/target/debug/liblivekit_ffi.a")
  set(RUST_LIB_RELEASE "${RUST_ROOT}/target/release/liblivekit_ffi.a")
  
  set_target_properties(livekit_ffi PROPERTIES
    IMPORTED_CONFIGURATIONS "Debug;Release;RelWithDebInfo;MinSizeRel"
    IMPORTED_LOCATION_DEBUG          "${RUST_LIB_DEBUG}"
    IMPORTED_LOCATION_RELEASE        "${RUST_LIB_RELEASE}"
    IMPORTED_LOCATION_RELWITHDEBINFO "${RUST_LIB_RELEASE}"
    IMPORTED_LOCATION_MINSIZEREL     "${RUST_LIB_RELEASE}"
    INTERFACE_INCLUDE_DIRECTORIES    "${RUST_ROOT}/livekit-ffi/include"
  )
endif()

file(GLOB_RECURSE RUST_SOURCES 
  "${RUST_ROOT}/livekit-ffi/src/*.rs"
  "${RUST_ROOT}/livekit-ffi/Cargo.toml"
)

add_custom_command(
  OUTPUT "${RUST_LIB_DEBUG}" "${RUST_LIB_RELEASE}"
  COMMAND "${CMAKE_COMMAND}"
          -DCFG=$<CONFIG>
          -DRUST_ROOT=${RUST_ROOT}
          -DCARGO=${CARGO_EXECUTABLE}
          -DPROTOC_PATH=${Protobuf_PROTOC_EXECUTABLE}
          -P "${RUN_CARGO_SCRIPT}"
  WORKING_DIRECTORY "${RUST_ROOT}"
  DEPENDS ${RUST_SOURCES}
  COMMENT "Building Rust FFI via cargo"
  VERBATIM
)

add_custom_target(build_rust_ffi
  DEPENDS "${RUST_LIB_DEBUG}" "${RUST_LIB_RELEASE}"
)

if(UNIX AND NOT APPLE)
  if(NOT CMAKE_AR)
    find_program(CMAKE_AR ar REQUIRED)
  endif()
  add_custom_command(
    TARGET build_rust_ffi
    POST_BUILD
    COMMAND ${CMAKE_AR} -dv "${RUST_LIB_RELEASE}" protozero_plugin.o
    COMMENT "Removing protozero_plugin.o from liblivekit_ffi.a"
  )
endif()

add_library(livekit STATIC
  src/audio_frame.cpp
  src/audio_source.cpp
  src/audio_stream.cpp
  src/data_stream.cpp
  src/e2ee.cpp
  src/ffi_handle.cpp
  src/ffi_client.cpp
  src/ffi_client.h
  src/local_audio_track.cpp
  src/remote_audio_track.cpp
  src/room.cpp
  src/room_proto_converter.cpp
  src/room_proto_converter.h
  src/local_participant.cpp
  src/remote_participant.cpp
  src/stats.cpp
  src/track.cpp
  src/track_proto_converter.cpp
  src/track_proto_converter.h
  src/track_publication.cpp
  src/local_track_publication.cpp
  src/remote_track_publication.cpp
  src/rpc_error.cpp
  src/video_frame.cpp
  src/video_source.cpp
  src/video_stream.cpp
  src/local_video_track.cpp
  src/remote_video_track.cpp
  src/video_utils.cpp
  src/video_utils.h
)

target_sources(livekit PRIVATE $<TARGET_OBJECTS:livekit_proto>)

target_include_directories(livekit
  PUBLIC
    $<BUILD_INTERFACE:${LIVEKIT_ROOT_DIR}/include>
    $<INSTALL_INTERFACE:include>
  PRIVATE
    ${LIVEKIT_ROOT_DIR}/src
    ${RUST_ROOT}/livekit-ffi/include
    ${PROTO_BINARY_DIR}
    ${GENERATED_INCLUDE_DIR}
)

target_link_libraries(livekit
  PRIVATE
    livekit_ffi
    protobuf::libprotobuf
)

message(STATUS "Protobuf: version=${Protobuf_VERSION}; protoc=${Protobuf_PROTOC_EXECUTABLE}")

add_dependencies(livekit build_rust_ffi)

if(LIVEKIT_IS_TOPLEVEL)
  # Copy public headers to build/include
  add_custom_command(
    TARGET livekit POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            ${LIVEKIT_ROOT_DIR}/include
            ${CMAKE_BINARY_DIR}/include
    COMMAND ${CMAKE_COMMAND} -E copy
            ${GENERATED_INCLUDE_DIR}/build.h
            ${CMAKE_BINARY_DIR}/include/livekit/build.h
    COMMENT "Copying public headers to build/include"
  )

  # Copy livekit_ffi library to output directory alongside livekit library
  if(WIN32)
    # Copy both DLL and import library
    set(FFI_DLL_DEBUG     "${RUST_ROOT}/target/debug/livekit_ffi.dll")
    set(FFI_DLL_RELEASE   "${RUST_ROOT}/target/release/livekit_ffi.dll")
    set(FFI_IMPLIB_DEBUG  "${RUST_ROOT}/target/debug/livekit_ffi.dll.lib")
    set(FFI_IMPLIB_RELEASE "${RUST_ROOT}/target/release/livekit_ffi.dll.lib")
    
    # Copy DLL file
    add_custom_command(
      TARGET livekit POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy
              "$<IF:$<CONFIG:Debug>,${FFI_DLL_DEBUG},${FFI_DLL_RELEASE}>"
              "$<TARGET_FILE_DIR:livekit>/livekit_ffi.dll"
      COMMENT "Copying livekit_ffi.dll to output directory"
    )
    # Copy import library
    add_custom_command(
      TARGET livekit POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy
              "$<IF:$<CONFIG:Debug>,${FFI_IMPLIB_DEBUG},${FFI_IMPLIB_RELEASE}>"
              "$<TARGET_FILE_DIR:livekit>/livekit_ffi.dll.lib"
      COMMENT "Copying livekit_ffi.dll.lib to output directory"
    )
  else()
    set(FFI_LIB_DEBUG   "${RUST_ROOT}/target/debug/liblivekit_ffi.a")
    set(FFI_LIB_RELEASE "${RUST_ROOT}/target/release/liblivekit_ffi.a")
    add_custom_command(
      TARGET livekit POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy
              "$<IF:$<CONFIG:Debug>,${FFI_LIB_DEBUG},${FFI_LIB_RELEASE}>"
              "$<TARGET_FILE_DIR:livekit>/liblivekit_ffi.a"
      COMMENT "Copying liblivekit_ffi.a to output directory"
    )
  endif()

  # Copy third-party DLL and LIB dependencies to lib directory (Windows only)
  if(WIN32 AND LIVEKIT_USE_VCPKG)
    # Determine vcpkg directories based on configuration
    set(VCPKG_BIN_DIR_DEBUG "${LIVEKIT_ROOT_DIR}/vcpkg_installed/x64-windows/debug/bin")
    set(VCPKG_BIN_DIR_RELEASE "${LIVEKIT_ROOT_DIR}/vcpkg_installed/x64-windows/bin")
    set(VCPKG_LIB_DIR_DEBUG "${LIVEKIT_ROOT_DIR}/vcpkg_installed/x64-windows/debug/lib")
    set(VCPKG_LIB_DIR_RELEASE "${LIVEKIT_ROOT_DIR}/vcpkg_installed/x64-windows/lib")
    
    # List of required DLL files (runtime)
    set(REQUIRED_DLLS
      "libprotobuf.dll"
      # "libprotobuf-lite.dll"
      # "libprotoc.dll"
      "abseil_dll.dll"
    )
    
    # List of required LIB files (for linking)
    set(REQUIRED_LIBS
      "libprotobuf.lib"
      # "libprotobuf-lite.lib"
      # "libprotoc.lib"
      "abseil_dll.lib"
    )
    
    # Copy DLLs to the lib output directory
    foreach(DLL_FILE ${REQUIRED_DLLS})
      add_custom_command(
        TARGET livekit POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
                "$<IF:$<CONFIG:Debug>,${VCPKG_BIN_DIR_DEBUG}/${DLL_FILE},${VCPKG_BIN_DIR_RELEASE}/${DLL_FILE}>"
                "$<TARGET_FILE_DIR:livekit>/${DLL_FILE}"
        COMMENT "Copying ${DLL_FILE} to lib directory"
        VERBATIM
      )
    endforeach()
    
    # Copy LIBs (import libraries) to the lib output directory
    foreach(LIB_FILE ${REQUIRED_LIBS})
      add_custom_command(
        TARGET livekit POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
                "$<IF:$<CONFIG:Debug>,${VCPKG_LIB_DIR_DEBUG}/${LIB_FILE},${VCPKG_LIB_DIR_RELEASE}/${LIB_FILE}>"
                "$<TARGET_FILE_DIR:livekit>/${LIB_FILE}"
        COMMENT "Copying ${LIB_FILE} to lib directory"
        VERBATIM
      )
    endforeach()
    
    message(STATUS "Third-party DLLs will be copied to lib directory: ${REQUIRED_DLLS}")
    message(STATUS "Third-party LIBs will be copied to lib directory: ${REQUIRED_LIBS}")
  endif()
endif()

if(APPLE)
  if(NOT DEFINED CMAKE_OSX_DEPLOYMENT_TARGET OR CMAKE_OSX_DEPLOYMENT_TARGET VERSION_LESS "12.3")
    set(CMAKE_OSX_DEPLOYMENT_TARGET "12.3" CACHE STRING "Minimum macOS version" FORCE)
  endif()

  find_library(FW_COREAUDIO      CoreAudio      REQUIRED)
  find_library(FW_AUDIOTOOLBOX   AudioToolbox   REQUIRED)
  find_library(FW_COREFOUNDATION CoreFoundation REQUIRED)
  find_library(FW_SECURITY       Security       REQUIRED)
  find_library(FW_COREGRAPHICS   CoreGraphics   REQUIRED)
  find_library(FW_COREMEDIA      CoreMedia      REQUIRED)
  find_library(FW_VIDEOTOOLBOX   VideoToolbox   REQUIRED)
  find_library(FW_AVFOUNDATION   AVFoundation   REQUIRED)
  find_library(FW_COREVIDEO      CoreVideo      REQUIRED)
  find_library(FW_FOUNDATION     Foundation     REQUIRED)
  find_library(FW_APPKIT         AppKit         REQUIRED)
  find_library(FW_QUARTZCORE     QuartzCore     REQUIRED)
  find_library(FW_OPENGL         OpenGL         REQUIRED)
  find_library(FW_IOSURFACE      IOSurface      REQUIRED)
  find_library(FW_METAL          Metal          REQUIRED)
  find_library(FW_METALKIT       MetalKit       REQUIRED)
  find_library(FW_SCREENCAPTUREKIT ScreenCaptureKit REQUIRED)

  target_link_libraries(livekit PUBLIC
    ${FW_COREAUDIO}
    ${FW_AUDIOTOOLBOX}
    ${FW_COREFOUNDATION}
    ${FW_SECURITY}
    ${FW_COREGRAPHICS}
    ${FW_COREMEDIA}
    ${FW_VIDEOTOOLBOX}
    ${FW_AVFOUNDATION}
    ${FW_COREVIDEO}
    ${FW_FOUNDATION}
    ${FW_APPKIT}
    ${FW_QUARTZCORE}
    ${FW_OPENGL}
    ${FW_IOSURFACE}
    ${FW_METAL}
    ${FW_METALKIT}
    ${FW_SCREENCAPTUREKIT}
  )

  target_link_options(livekit INTERFACE "LINKER:-ObjC")
endif()

if(Protobuf_VERSION VERSION_GREATER_EQUAL 6.0)
  find_package(absl CONFIG QUIET)
  if(NOT absl_FOUND)
    find_package(Abseil QUIET)
  endif()

  if(absl_FOUND)
    target_link_libraries(livekit PRIVATE
      absl::log
      absl::check
      absl::strings
      absl::base
    )
  elseif(Abseil_FOUND)
    target_link_libraries(livekit PRIVATE
      Abseil::log
      Abseil::check
      Abseil::strings
      Abseil::base
    )
  else()
    message(FATAL_ERROR
      "Protobuf ${Protobuf_VERSION} requires Abseil but no CMake package was found.\n"
      "Install Abseil or use Protobuf < 6.")
  endif()
else()
  message(STATUS "Protobuf < 6 detected; skipping Abseil linking.")
endif()

if(WIN32)
  target_link_libraries(livekit PUBLIC
    ntdll
    userenv
    winmm
    iphlpapi
    msdmo
    dmoguids
    wmcodecdspuuid
    ws2_32
    secur32
    bcrypt
    crypt32
  )
endif()

if(UNIX AND NOT APPLE)
  find_package(OpenSSL REQUIRED)
  target_link_libraries(livekit PUBLIC OpenSSL::SSL OpenSSL::Crypto)
endif()

if(MSVC)
  target_compile_options(livekit PRIVATE /permissive- /Zc:__cplusplus /W4)
else()
  target_compile_options(livekit PRIVATE -Wall -Wextra -Wpedantic)
endif()



if(LIVEKIT_BUILD_EXAMPLES)
  add_subdirectory(examples)
endif()

add_custom_target(clean_generated
  COMMAND ${CMAKE_COMMAND} -E rm -rf "${PROTO_BINARY_DIR}"
  COMMENT "Clean generated protobuf files"
  VERBATIM
)

add_custom_target(cargo_clean
  COMMAND "${CARGO_EXECUTABLE}" clean
  WORKING_DIRECTORY "${RUST_ROOT}"
  COMMENT "Clean Rust target directory via cargo"
  VERBATIM
)

add_custom_target(clean_all
  COMMAND ${CMAKE_COMMAND} --build "${LIVEKIT_BINARY_DIR}" --target clean || true
  COMMAND ${CMAKE_COMMAND} -E chdir "${RUST_ROOT}" "${CARGO_EXECUTABLE}" clean || true
  COMMAND ${CMAKE_COMMAND} -E rm -rf "${PROTO_BINARY_DIR}" || true
  COMMAND ${CMAKE_COMMAND} -E chdir "${LIVEKIT_ROOT_DIR}" ${CMAKE_COMMAND} -E rm -rf "${LIVEKIT_BINARY_DIR}" || true
  COMMENT "Full clean: CMake outputs + Rust target + generated protos"
  VERBATIM
)